plugins {
	id 'application'
	id 'java'
	id 'eclipse'
	id 'de.fuerstenau.buildconfig' version '1.1.8'
}

mainClassName = 'za.ac.sun.cs.deepsea.DEEPSEA'
group = 'za.ac.sun.cs'
ext.greenRepoUrl = 'http://repo.jacogeld.org:8081/repository/snapshots/'

def getVersionName = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

version = getVersionName()

repositories {
	jcenter()
	maven { url greenRepoUrl }
	// mavenLocal()
}

buildConfig {
	sourceSets {
		main {
			packageName = 'za.ac.sun.cs.deepsea'
		}
  	}
}

dependencies {
	compile files("${System.properties['java.home']}/../lib/tools.jar")
	// compile files("lib/green.jar")
	compile group: 'za.ac.sun.cs', name: 'green', version: '0.0.1-SNAPSHOT'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
	compile group: 'org.apfloat', name: 'apfloat', version: '1.8.2'
	compile group: 'redis.clients', name: 'jedis', version: '2.4.2'
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

javadoc {
	options.noTree true
	options.noIndex true
	options.noHelp true
	options.noNavBar true
}

startScripts {
	def u = unixStartScriptGenerator
	String us = u.template.asString().replace('DEFAULT_JVM_OPTS=${defaultJvmOpts}','DEFAULT_JVM_OPTS="-Djava.library.path=/usr/local/lib"')
    u.template = resources.text.fromString(us)
    
    // TODO repeat above for Windows 
}
